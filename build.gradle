buildscript {
    repositories {
        maven { url = 'https://files.minecraftforge.net/maven' }
        maven { url = 'https://repo.spongepowered.org/repository/maven-public/' }
        mavenCentral()
    }
    dependencies {
        classpath group: 'net.minecraftforge.gradle', name: 'ForgeGradle', version: '4.+', changing: true
        classpath group: 'org.spongepowered', name: 'mixingradle', version: '0.7-SNAPSHOT'
    }
}

apply plugin: 'net.minecraftforge.gradle'
apply plugin: 'eclipse'
apply plugin: 'maven-publish'
apply plugin: 'org.spongepowered.mixin'

version = mod_version
archivesBaseName = mod_name

sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8

minecraft {
    mappings channel: 'snapshot', version: "$mappings_version"
    accessTransformer = file('src/main/resources/META-INF/accesstransformer.cfg')

    runs {
        client {
            workingDirectory project.file('run')
            arg "-mixin.config=osv.mixins.json"
            property 'forge.logging.markers', 'SCAN,REGISTRIES,REGISTRYDUMP'
            property 'forge.logging.console.level', 'debug'

            mods {
                osv {
                    source sourceSets.main
                }
            }
        }

        server {
            workingDirectory project.file('runServer')
            arg "-mixin.config=osv.mixins.json"
            property 'forge.logging.markers', 'SCAN,REGISTRIES,REGISTRYDUMP'
            property 'forge.logging.console.level', 'debug'

            mods {
                osv {
                    source sourceSets.main
                }
            }
        }
    }
}

allprojects {
    repositories {
        maven { url 'https://www.cursemaven.com' }
        maven { url = 'https://www.dogforce-games.com/maven/' }
    }
}

dependencies {
    minecraft "net.minecraftforge:forge:$mc_version-$forge_version"
    implementation fg.deobf("curse.maven:CatLib-513391:$catlib_maven_forge_version")
    implementation 'org.jetbrains:annotations:16.0.2'

    // We have a lot of large objects. This improves readability.
    compileOnly 'org.projectlombok:lombok:1.18.20'
    annotationProcessor 'org.projectlombok:lombok:1.18.20'
    annotationProcessor 'org.spongepowered:mixin:0.8.2:processor'
}

jar {
    manifest {
        attributes([
            "Specification-Title": "$mod_id",
            "Specification-Vendor": "$mod_vendor",
            "Specification-Version": "$mod_version",
            "Implementation-Title": "$mod_name",
            "Implementation-Version": "$mod_version",
            "Implementation-Vendor" :"$mod_vendor",
            "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ"),
            "MixinConfigs": "osv.mixins.json"
        ])
    }
}

processResources {
    filesMatching(['**/mods.toml', '**/pack.mcmeta']) {
        expand project.properties
    }
}

jar.finalizedBy('reobfJar')

mixin {
    add sourceSets.main, "osv.refmap.json"
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifact jar
        }
    }
    repositories {
        maven {
            url "file:///${project.projectDir}/mcmodsrepo"
        }
    }
}